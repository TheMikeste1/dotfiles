# Set the directory we want to store zinit and plugins
readonly ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"

# Download Zinit, if it's not there yet
if [[ ! -d "$ZINIT_HOME" ]]; then
  echo "Zinit not found, installing. . ."
  mkdir -p "$(dirname "$ZINIT_HOME")"
  git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi

# Source/Load zinit
source "${ZINIT_HOME}/zinit.zsh"

readonly COMPLETIONS_DIR="$ZSH_CACHE_DIR/completions"
if [[ ! -d "$COMPLETIONS_DIR" ]]; then
  mkdir -p "$COMPLETIONS_DIR"
fi

# Add in zsh plugins
zinit ice blockf atpull'zinit creinstall -q .'
zinit light zsh-users/zsh-completions
zinit light zsh-users/zsh-autosuggestions
export ZSH_AUTOSUGGEST_USE_ASYNC=true
zinit light olets/zsh-abbr
zinit light Aloxaf/fzf-tab

# Postpone loading fzf so it doesn't get messed up by vi mode
# zinit ice lucid wait
zinit snippet OMZP::fzf
zinit snippet OMZP::rust

# Add in snippets
zinit snippet OMZP::sudo
zinit snippet OMZP::command-not-found

# Additional completions
zstyle ':completion:*:*:git:*' script ~/.zsh/git-completion.bash
fpath=("$HOME"/.zsh $fpath)
fpath+=("$HOME"/.zfunc)

# Completion styling
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}' # Set case insensitive
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' menu no
zstyle ':fzf-tab:complete:cd:*' fzf-preview "ls --color \$realpath"
zstyle ':fzf-tab:complete:git*:*' fzf-preview

if [[ -f "$HOME"/.bash_aliases ]]; then
  source "$HOME"/.bash_aliases
fi

# Load completions
autoload -Uz compinit
# Load compinit from the cache if it's been less than 24 hours
setopt extendedglob local_options
if [[ -n "${ZDOTDIR:-$HOME}"/.zcompdump(#qN.mh+24) ]]; then
  echo "Refreshing compinit cache. . ."
  compinit
  touch "${ZDOTDIR:-$HOME}"/.zcompdump
else
  compinit -C
fi
unsetopt extendedglob local_options

zinit cdreplay -q
fpath+=("$COMPLETIONS_DIR")

function refresh_eval_cache() {
  oh-my-posh init zsh --config "$HOME/.config/oh-my-posh/theme_default.yaml" > "$EVAL_CACHE_DIR/oh-my-posh_init.zsh"
  mise activate zsh > "$EVAL_CACHE_DIR/mise_init.zsh"

  touch "$EVAL_CACHE_DIR"/.last_cache
}

function try_generate_comp_cache() {
  if ! command -v $1 >/dev/null 2>&1
  then
    echo "$1 is not installed; not generating completions"
    return 1
  fi

  command $@ > "$COMPLETIONS_DIR/_$1"
  return 0
}

function autoload_completion() {
  autoload -Uz _$1 && typeset -g -A _comps && _comps[$1]=_$1
}

function refresh_comp_cache() {
  try_generate_comp_cache chezmoi completion zsh
  try_generate_comp_cache fzf --zsh
  try_generate_comp_cache lefthook completion zsh
  try_generate_comp_cache mdbook completions zsh
  try_generate_comp_cache mise completions zsh
  # try_generate_comp_cache oh-my-posh completion zsh
  try_generate_comp_cache uv generate-shell-completion zsh
  try_generate_comp_cache uvx --generate-shell-completion zsh
}

function source_eval_cache() {
  source "$EVAL_CACHE_DIR/oh-my-posh_init.zsh"
  source "$EVAL_CACHE_DIR/mise_init.zsh"
}

setopt extendedglob local_options
if [ ! -f "$EVAL_CACHE_DIR"/.last_cache ] || [[ -n "$EVAL_CACHE_DIR"/.last_cache(#qN.mh+24) ]]; then
  echo "Refreshing eval cache. . ."
  refresh_eval_cache
  # Need to source before we set up the comp cache
  source_eval_cache
  refresh_comp_cache
else
  source_eval_cache
fi
unsetopt extendedglob local_options

for f in "$COMPLETIONS_DIR"/*
do
  f="${f##*/}"
  autoload -Uz _$f && typeset -g -A _comps && _comps[$f]=_$f
done

zinit light zsh-users/zsh-syntax-highlighting # Must be loaded last to load all completions

# Keybindings
bindkey -e
bindkey '^p' history-search-backward
bindkey '^n' history-search-forward
bindkey '^[w' kill-region
bindkey '^[^l' autosuggest-accept
bindkey '^h' backward-word
bindkey '^l' forward-word
bindkey '^[[1;5C' backward-word
bindkey '^[[1;5D' forward-word
bindkey '^[[1~' beginning-of-line
bindkey '^[[4~' end-of-line
bindkey '^[[3~' delete-char
exit_zsh() { exit }
zle -N exit_zsh
bindkey '^D' exit_zsh

autoload -z edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line

# Vi insert
bindkey -M viins -r '\e\e'
bindkey -M viins '^[`' sudo-command-line
# In Vi mode, we need these for abbr to work
# <https://zsh-abbr.olets.dev/advanced.html#alternative-keymaps>
bindkey -M viins " " abbr-expand-and-insert
bindkey -M viins "^ " magic-space

# Vi normal/command and visual
function zvm_after_lazy_keybindings() {
  # Replace sudo bindings so as to not slow down vi mode
  bindkey -M vicmd '^[`' sudo-command-line
  bindkey -M vicmd -r '\e\e'
}

# Emacs
bindkey -M emacs -r '\e\e'
bindkey -M emacs '^[`' sudo-command-line
bindkey -M emacs " " abbr-expand-and-insert
bindkey -M emacs "^ " magic-space

# History
export HISTSIZE=5000
export HISTFILE="$HOME/.zsh_history"
export SAVEHIST="$HISTSIZE"
export HISTDUP=erase
setopt appendhistory
setopt sharehistory
setopt hist_ignore_space
setopt hist_ignore_all_dups
setopt hist_save_no_dups
setopt hist_ignore_dups
setopt hist_find_no_dups

# TODO: Customize
# Source: <https://www.reddit.com/r/zsh/comments/ass2tc/gitadd_completion_with_full_paths_listed_at_once/>
__git_status_files() {
  # TODO: Update to use porcelain instead of raw status
  local -a status_files=( "${(@f)"$(git status -s)"}" )
  local -a staged_files
  local -a unstaged_files
  for entry in "${status_files[@]}"; do
    if [[ "$entry" == '' ]];
    then
      continue
    fi

    local stts="${entry[1,3]}"
    local file="${entry[4,-1]}"

    if [[ "${stts[1]}" != ' ' ]] && [[ "${stts[1]}"  != '?' ]];
    then
      staged_files+=$file
    fi

    if [[ "${stts[2]}" != ' ' ]];
    then
      unstaged_files+=$file
    fi
  done

  _describe -t unstaged 'Unstaged' unstaged_files && ret=0
  _describe -t staged 'Staged' staged_files && ret=0

  return "$ret"
}

__git_staged_files() {
  echo "__git_staged_files" # Echo to determine when this is actually called
  local -a staged_files=( "${"${(0)"$(git diff-index -z --name-only --no-color --cached HEAD)"}"}" )

  _describe -t staged 'Staged files' staged_files && ret=0
  return "$ret"
}

__git_modified_files() {
  # TODO: Update to use porcelain instead of raw status
  local -a status_files=( "${(@f)"$(git status -s)"}" )
  local -a unstaged_files
  for entry in "${status_files[@]}"; do
    if [[ "$entry" == '' ]];
    then
      continue
    fi

    local stts="${entry[1,3]}"
    local file="${entry[4,-1]}"

    if [[ "${stts[2]}" != ' ' ]];
    then
      unstaged_files+=$file
    fi
  done

  _describe -t unstaged 'Unstaged' unstaged_files && ret=0

  return "$ret"
}

__git_treeish-to-index_files() {
  __git_staged_files
}

__git_other_files() {
  return 0
}
