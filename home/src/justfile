# https://just.systems

set unstable := true

nproc := `echo $(( $(nproc) / 2 ))`
cmake_generator := '$(command -v ninja >/dev/null 2>&1 && echo "Ninja" || echo "Unix Makefiles")'

default:
    -just cmake
    -just ninja-build
    -just neovim
    -just tmux

all: default
    -just llvm

[no-exit-message]
[script]
_git-ensure repo url branch:
    if [ ! -d "./{{ repo }}" ]; then
        git clone --recursive --single-branch --branch {{ branch }} --jobs={{ nproc }} -- {{ url }} ./{{ repo }}
    else
        git -C ./{{ repo }} fetch
        readonly local_commit=$(git -C ./{{ repo }} rev-parse HEAD)
        readonly remote_commit=$(git -C ./{{ repo }} rev-parse @{u})
        if [ "$local_commit" = "$remote_commit" ]; then
          echo "{{ repo }} does not require updating"
          exit 1
        fi

        git -C ./{{ repo }} pull --ff-only
        git -C ./{{ repo }} submodule update --init --recursive --jobs={{ nproc }}
    fi

[working-directory("cmake")]
cmake: (_git-ensure "cmake" "git@github.com:Kitware/CMake.git" "release")
    cmake -S . -B build -DCMAKE_BUILD_TYPE=Release -DCMAKE_GENERATOR="{{ cmake_generator }}" -DCMAKE_INSTALL_PREFIX=$HOME/.local
    cmake --build build --parallel={{ nproc }}
    cmake --build build --target install

[working-directory("ninja")]
ninja-build: (_git-ensure "ninja" "git@github.com:ninja-build/ninja.git" "release")
    cmake -S . -B build -DCMAKE_BUILD_TYPE=Release -DCMAKE_GENERATOR="{{ cmake_generator }}" -DBUILD_TESTING=OFF -DCMAKE_INSTALL_PREFIX=$HOME/.local
    cmake --build build --parallel={{ nproc }}
    cmake --build build --target install

[working-directory("neovim")]
neovim: (_git-ensure "neovim" "git@github.com:neovim/neovim.git" "master")
    make distclean
    make deps
    make CMAKE_BUILD_TYPE=Release CMAKE_INSTALL_PREFIX=$HOME/.local
    make install

[working-directory("llvm-project")]
llvm: (_git-ensure "llvm-project" "git@github.com:llvm/llvm-project.git" "main")
    cmake -S llvm -B build -DCMAKE_BUILD_TYPE=Release -DLLVM_ENABLE_PROJECTS="clang;clang-tools-extra;lldb" -DCMAKE_INSTALL_PREFIX=$HOME/.local
    cmake --build build --parallel={{ nproc }}
    cmake --build build --target install

[working-directory("tmux")]
tmux: (_git-ensure "tmux" "git@github.com:tmux/tmux.git" "master")
    sh autogen.sh
    ./configure --prefix=$HOME/.local
    make
    make install
